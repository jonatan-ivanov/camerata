plugins {
    id 'application' //brings in the java and the distribution plugins
}

apply from: 'gradle/idea.gradle'

sourceCompatibility = 11
targetCompatibility = 11
mainClassName = 'io.github.camerata.App'

repositories {
    jcenter() // should be the superset of mavenCentral()
}
dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.+'
    compileOnly 'org.projectlombok:lombok:1.+'

    implementation 'ch.qos.logback:logback-classic:1.2.+' // native implementation of https://www.slf4j.org/manual.html

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.+'
    testImplementation 'org.assertj:assertj-core:3.+'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.+'
}

dependencyLocking {
    lockAllConfigurations()
}

task resolveAndLockAll {
    doFirst {
        assert gradle.startParameter.writeDependencyLocks
    }
    doLast {
        configurations.findAll { it.canBeResolved }.each { it.resolve() }
    }
}

test {
    useJUnitPlatform()
}

task cleanAll {
    dependsOn clean, cleanIdea, cleanIdeaWorkspace
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = new groovy.json.JsonSlurper().parseText('https://services.gradle.org/versions/current'.toURL().text).version
}
